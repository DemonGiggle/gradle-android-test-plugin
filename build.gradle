buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.2'
  }
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'nexus'

group = 'com.squareup'
version = '1.0-SNAPSHOT'

dependencies {
  repositories {
    mavenCentral()
  }

  compile gradleApi()
  compile localGroovy()
  compile 'com.android.tools.build:gradle:0.5.+'

  testCompile 'junit:junit:4.10'
  testCompile 'org.easytesting:fest-assert-core:2.0M10'
}

install {
  repositories.mavenInstaller {
    pom.artifactId = 'gradle-android-test'
  }
}
uploadArchives {
  repositories.mavenDeployer {
    pom.artifactId = 'gradle-android-test'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.7'
}

modifyPom {
  project {
    name 'Gradle Android Unit Test Plugin'
    description "A Gradle plugin which enables good 'ol fashioned unit tests for Android builds."
    url 'https://github.com/square/gradle-android-test-plugin'
    inceptionYear '2013'

    scm {
      url 'https://github.com/square/gradle-android-test-plugin'
      connection 'scm:git:git://github.com/square/gradle-android-test-plugin.git'
      developerConnection 'scm:git:ssh://git@github.com/square/gradle-android-test-plugin.git'
    }

    licenses {
      license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        distribution 'repo'
      }
    }

    organization {
      name 'Square, Inc.'
      url 'http://squareup.com'
    }
  }
}
